plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.openapi.generator" version "5.1.0"
	id 'java'
}

apply plugin: 'idea'
group = 'com.theindianmaharajatours'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

compileJava.dependsOn('openApiGenerate')

idea {
	module {
		outputDir file('build/classes/java/main')
		testOutputDir file('build/classes/test')
	}
}

sourceSets {
	main {
		java {
			srcDir "${rootDir}/backend-service/src/main/java"
		}
	}
}
task cleanGeneratedCode(type: Delete){
	delete file("src/main/java/generated")
}

openApiValidate {
	inputSpec = rootProject.file('openAPISpec/openApi.yaml').toString()
}

openApiGenerate {
	generatorName = "spring"
	library = "spring-boot"
	inputSpec = rootProject.file('openAPISpec/openApi.yaml').toString()
	outputDir = file('src/main/java/generated').toString()
	configOptions = [
			basePackage          : "com.generated.code",
			configPackage        : "com.generated.code.config",
			apiPackage           : "com.generated.code.api",
			modelPackage         : "com.generated.code.model",
			java8                : "true",
			interfaceOnly        : "true",
			useOptional          : "true",
			sourceFolder		 : "."
	]
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'javax.mail:mail:1.4.1'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'
	implementation 'io.springfox:springfox-swagger2:3.0.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
	implementation 'org.mapstruct:mapstruct:1.4.2.Final'
	implementation 'javax.validation:validation-api:2.0.1.Final'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}
